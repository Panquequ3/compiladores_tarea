package analizadorLexico;
import java_cup.runtime.*;

parser code {:
    java.util.HashMap<String, Object> symtab = new java.util.HashMap<>();

    public static void main(String[] args) throws Exception {
        Analizador lexer = new Analizador(new java.io.InputStreamReader(System.in));
        Parser parser = new Parser(lexer);
        parser.parse();
        System.out.println("Parseo Completo");
    }
:};

scan with {:
    return getScanner().next_token();
:};


terminal String PARTIR, CREAR, INSERTAR, ELIMINAR, DATO, ASIGNAR, FINALIZAR, VER;
terminal String PARENTESIS_IZQ, PARENTESIS_DER, COMA, IGUAL, DESCONOCIDO;

terminal String IDENTIFICADOR; // Identificadores de listas
terminal String IDVALOR;       // Identificadores generales
terminal Integer NUMERO;

nonterminal S, inst, inst_item, inst_asign, inst_crear, inst_dato, inst_eliminar, inst_insertar, inst_ver, x;

start with S;

S ::= PARTIR inst FINALIZAR;

inst ::= inst inst_item
       | inst_item;

inst_item ::= inst_asign
            | inst_crear
            | inst_dato
            | inst_eliminar
            | inst_insertar
            | inst_ver;

inst_asign ::= ASIGNAR IDVALOR:idn IGUAL x:idx 
{:
    Object val4 = (idx instanceof String && symtab.containsKey(idx)) ? symtab.get(idx) : idx;
    symtab.put(idn, val4);
:};

inst_crear ::= CREAR
    PARENTESIS_IZQ
    IDENTIFICADOR:id COMA x:idx1 COMA x:idx2 COMA x:idx3 COMA x:idx4 COMA x:idx5 COMA x:idx6 COMA x:idx7 COMA x:idx8
    PARENTESIS_DER
{:
    java.util.ArrayList<Object> params = new java.util.ArrayList<>();
    params.add(idx1); params.add(idx2); params.add(idx3); params.add(idx4);
    params.add(idx5); params.add(idx6); params.add(idx7); params.add(idx8);
    symtab.put(id, params);
:};

inst_dato ::= DATO
    PARENTESIS_IZQ
    IDENTIFICADOR:id COMA NUMERO:num
    PARENTESIS_DER
{:
    java.util.ArrayList<Object> arr = (java.util.ArrayList<Object>) symtab.get(id);
    System.out.println(arr.get(num));
:};

inst_eliminar ::= ELIMINAR
    PARENTESIS_IZQ
    IDENTIFICADOR:id COMA NUMERO:num
    PARENTESIS_DER
{:
    java.util.ArrayList<Object> l = (java.util.ArrayList<Object>) symtab.get(id);
    l.remove((int) num);
:};

inst_insertar ::= INSERTAR
    PARENTESIS_IZQ
    IDENTIFICADOR:id COMA x:idx COMA NUMERO:num
    PARENTESIS_DER
{:
    java.util.ArrayList<Object> l = (java.util.ArrayList<Object>) symtab.get(id);
    int pos = num;
    Object val = idx;
    if (val instanceof String && symtab.containsKey(val)) {
        val = symtab.get(val);
    }
    l.set(pos, val);
    symtab.put(id,l);
:};

inst_ver ::= VER
    PARENTESIS_IZQ
    IDENTIFICADOR:id
    PARENTESIS_DER
{:
    java.util.ArrayList<Object> l = (java.util.ArrayList<Object>) symtab.get(id);
    for (Object elemento : l) {
        System.out.print(elemento + " ");
    }
    System.out.println();
:};

x ::= IDVALOR:idv {: RESULT = idv; :}
    | NUMERO:num {: RESULT = num; :};